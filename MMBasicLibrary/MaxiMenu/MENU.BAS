100 '* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
105 '*                                                           *
110 '*                    M A X I - M E N U                      *
115 '*                                                           *
120 '*                       Version 1.00                        *
125 '*                                                           *
130 '* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
135 '
140 'Programmed JUNE 2011 by Nickolas Marentes
145 '
150 'EMAIL: nickma@optusnet.com.au
155 '
160 '----------------------- INSTRUCTIONS ------------------------
165 '
170 'MENU MODE:
175 '
180 'ARROW KEYS = Move menu pointer to select icon
185 'E          = Edit icon properties
190 'I          = Enter the Icon Editor
195 'Q          = Quit program and return to BASIC
200 'ENTER      = Start up selected icon/program
205 '
210 'ICON EDITOR MODE:
215 '
220 'ARROW KEYS = Move pixel pointer to select a pixel
225 'SPACEBAR   = Set or reset currently selected pixel
230 'PAGE UP    = Move to next icon (max 31)
235 'PAGE DOWN  = Move to previous icon (min 0)
240 'ENTER      = Exit Icon Editor and save all changes to SD
245 'ESC        = Clear current icon (must be presses twice)
250 'CTRL       = When held down with Arrow keys, will shift the
255 '             entire icon (all pixels) within the icon frame.
260 '             When held down with PGUP and PGDOWN, will move
265 '             to the next icon in increments of 10.
270 '-------------------------------------------------------------
275 '
280 ' INITIALIZE DATA FILES
285 '
290 CLS:DIM I(32,16),M$(20),I$(16):OPTION ERROR CONTINUE
295 OPEN "ICONS.DAT" FOR INPUT AS #1
300 FOR X=0 TO 31:FOR Y=0 TO 15:INPUT #1,I(X,Y):NEXT Y:NEXT X:CLOSE #1
305 OPEN "MENU.DAT" FOR INPUT AS #2
310 IF MM.ERRNO=0 THEN 330 ELSE IF MM.ERRNO=6 THEN 320
315 CLS:PRINT"SD I/O ERROR":END
320 OPEN "MENU.DAT" FOR OUTPUT AS #2
325 FOR X=0 TO 19:PRINT #2,STRING$(30,32):NEXT X:CLOSE #2:GOTO 305
330 FOR X=0 TO 19:INPUT #2,M$(X):NEXT X:CLOSE #2
335 '
340 ' SETUP MAIN MENU SCREEN
345 '
350 CLS:LINE (170,0)-(310,26),1,BF:LINE (0,12)-(480,14),1,BF
355 LOCATE 190,10:PRINT"M A X I - M E N U";
360 FOR Y=10 TO 21:FOR X=190 TO 291:PIXEL(X,Y)=NOT(PIXEL(X,Y)):NEXT X:NEXT Y
365 '
370 FOR Y=53 TO 323 STEP 90:FOR X=40 TO 400 STEP 90
375 LINE (X,Y)-(X+38,Y+39),1,B:NEXT X:NEXT Y
380 LINE (0,400)-(480,402),1,BF:LOCATE 68,409
385 PRINT"[E]DIT MENU ENTRY    [I]CON EDITOR     [Q]UIT MENU SYSTEM";
390 '
395 I=0:E=73:F=57
400 IF M$(I)="" THEN 420 ELSE X=E:Y=F
405 LABEL$=MID$(M$(I),3,12):ICON=VAL(LEFT$(M$(I),2))
410 GOSUB 1195:LOCATE E-50,F+45:PRINT LABEL$
415 '
420 E=E+90:I=I+1
425 IF E>433 THEN E=73:F=F+90
430 IF I<20 THEN 400
435 '
440 ' MOVE ICON CURSOR
445 '
450 X=39:Y=52:A=0:F=0
455 LINE (X,Y)-(X+40,Y+41),F,B:PAUSE 100:F=F XOR 1:IF F=0 THEN 455
460 K$=UCASE$(INKEY$)
465 IF K$="4" AND X>85 THEN X=X-90:A=A-1
470 IF K$="6" AND X<355 THEN X=X+90:A=A+1
475 IF K$="8" AND Y>52 THEN Y=Y-90:A=A-5
480 IF K$="2" AND Y<322 THEN Y=Y+90:A=A+5
485 IF K$="I" THEN GOSUB 1145:GOTO 710
490 IF K$="Q" THEN GOSUB 1145:PRINT"MAXIMITE BASIC":END
495 IF K$="E" THEN 560
500 IF ASC(K$)=13 AND M$(A)<>"" THEN 525
505 GOTO 455
510 '
515 ' RUN SELECTED PROGRAM
520 '
525 CLS:Z=22
530 IF MID$(M$(A),Z,1)=" " THEN Z=Z-1:IF Z>15 THEN 530
535 A$=MID$(M$(A),15,Z-14)
540 OPEN "MBOOT.BAS" FOR OUTPUT AS #3
545 PRINT #3,"10 CHDIR"+CHR$(34)+MID$(M$(A),23,8)+CHR$(34)
550 PRINT #3,"20 RUN"+CHR$(34)+A$+CHR$(34):CLOSE #3
555 RUN"MBOOT.BAS"
560 '
565 ' ENTER NEW ICON DATA
570 '
575 LINE (X,Y)-(X+40,Y+41),1,B:LINE (X-1,Y-1)-(X+41,Y+42),1,B
580 GOSUB 700:INPUT"ENTER ICON LABEL (12 CHRS OR ## TO DELETE):";A$
585 IF A$="" THEN 350 ELSE IF A$="##" THEN M$(A)="":GOTO 685
590 IF LEN(A$)>12 THEN 575
595 B$=LEFT$(STRING$((12-LEN(A$))/2,32)+A$+"            ",12)
600 GOSUB 700:INPUT"ENTER FILENAME (8 CHRS):";A$
605 IF A$="" THEN CLS:GOTO 350
610 IF LEN(A$)>8 THEN 600
615 B$=B$+LEFT$(A$+"        ",8)
620 GOSUB 700:INPUT"ENTER SUBDIRECTORY (8 CHRS):";A$
625 IF LEN(A$)>8 THEN 620
630 B$=B$+A$
635 LINE (15,45)-(465,380),0,BF:LINE (15,45)-(465,380),1,B
640 GOSUB 700:LOCATE 205,55:PRINT "ICON BROWSER":X=60:W=80
645 FOR ICON=0 TO 31:Y=W:GOSUB 1195
650 LOCATE X-25,Y+10:PRINT ICON
655 X=X+55:IF X>450 THEN X=60:W=W+75
660 NEXT ICON
665 GOSUB 700:INPUT"ENTER ICON NUMBER (0-31):";A$
670 IF A$="" THEN 350
675 IF VAL(A$)<0 OR VAL(A$)>31 THEN 665
680 M$(A)=RIGHT$("0"+A$,2)+B$
685 OPEN "MENU.DAT" FOR OUTPUT AS #2
690 FOR Z=0 TO 19:PRINT #2,M$(Z):NEXT Z:CLOSE #2
695 LINE (X-1,Y-1)-(X+41,Y+42),0,BF:GOSUB 700:GOTO 350
700 LINE (0,409)-(480,417),0,BF:A$="":LOCATE 10,409:PAUSE 50:RETURN
705 '
710 '------------  I C O N   E D I T O R  ------------
715 '
720 A=VAL(LEFT$(M$(A),2))
725 CLS
730 LOCATE 177,5:PRINT"I C O N   E D I T O R"
735 LOCATE 63,400:PRINT"ICON NUMBER:";A
740 '
745 ' CREATE PIXEL ARRAY
750 '
755 FOR B=0 TO 15:C=I(A,B):I$(B)=""
760 D=C*.5
765 IF D=0 THEN I$(B)=STRING$(16-LEN(I$(B)),"0")+I$(B):GOTO 780
770 IF D>INT(D) THEN I$(B)="1"+I$(B):C=(C-1)/2:GOTO 760
775 I$(B)="0"+I$(B):C=D:GOTO 760
780 NEXT B
785 '
790 ' PLOT ICON GRID
795 '
800 FOR Y=32 TO 362 STEP 22:FOR X=64 TO 394 STEP 22
805 LINE (X,Y)-(X+22,Y+22),1,B:LINE (X+1,Y+1)-(X+21,Y+21),0,BF:NEXT X:NEXT Y
810 B=0:FOR Y=34 TO 364 STEP 22
815 C=1:FOR X=66 TO 396 STEP 22
820 IF MID$(I$(B),C,1)="1" THEN LINE (X,Y)-(X+18,Y+18),1,BF
825 C=C+1:NEXT X:B=B+1:NEXT Y
830 V=11:W=66:GOSUB 1110
835 '
840 ' CURSOR MOVEMENT CONTROL
845 '
850 X=0:Y=0:F=0
855 LINE (X*22+66,Y*22+34)-(X*22+84,Y*22+52),F,BF:PAUSE 70
860 F=F XOR 1:IF F=0 THEN 855 ELSE K$=UCASE$(INKEY$):K=ASC(K$)
865 IF K$=" " THEN GOSUB 990:V=11:W=66:GOSUB 1110:GOTO 855
870 IF K$="4" AND X>00 THEN GOSUB 1020:X=X-1
875 IF K$="6" AND X<15 THEN GOSUB 1020:X=X+1
880 IF K$="8" AND Y>00 THEN GOSUB 1020:Y=Y-1
885 IF K$="2" AND Y<15 THEN GOSUB 1020:Y=Y+1
890 IF K$="3" AND A>00 THEN GOSUB 1045:A=A-1:GOTO 725
895 IF K=19 AND A>9 THEN GOSUB 1045:A=A-10:GOTO 725
900 IF K$="9" AND A<31 THEN GOSUB 1045:A=A+1:GOTO 725
905 IF K=25 AND A<22 THEN GOSUB 1045:A=A+10:GOTO 725
910 IF K=13 THEN GOSUB 1045:GOTO 1075
915 IF K<>24 THEN 925
920 B$=I$(0):FOR Z=0 TO 14:I$(Z)=I$(Z+1):NEXT Z:I$(15)=B$:GOTO 800
925 IF K<>18 THEN 935
930 B$=I$(15):FOR Z=15 TO 1 STEP -1:I$(Z)=I$(Z-1):NEXT Z:I$(0)=B$:GOTO 800
935 IF K<>20 THEN 945
940 FOR Z=0 TO 15:I$(Z)=RIGHT$(I$(Z),15)+LEFT$(I$(Z),1):NEXT Z:GOTO 800
945 IF K<>22 THEN 955
950 FOR Z=0 TO 15:I$(Z)=RIGHT$(I$(Z),1)+LEFT$(I$(Z),15):NEXT Z:GOTO 800
955 IF K<>27 THEN 855 ELSE PAUSE 1000
960 IF ASC(INKEY$)=27 THEN FOR Z=0 TO 15:I(A,Z)=0:NEXT Z:GOTO 725
965 Z=Z+1:IF Z<1000 THEN 960 ELSE 855
970 GOTO 855
975 '
980 ' SET/RESET PIXEL
985 '
990 IF MID$(I$(Y),X+1,1)="1" THEN B$="0" ELSE B$="1"
995 A$="":FOR L=1 TO 16
1000 IF L=X+1 THEN A$=A$+B$ ELSE A$=A$+MID$(I$(Y),L,1)
1005 NEXT L:I$(Y)=A$
1010 LINE (X*22+65,Y*22+33)-(X*22+85,Y*22+53),1,B:PAUSE 100
1015 LINE (X*22+65,Y*22+33)-(X*22+85,Y*22+53),0,B
1020 F=VAL(MID$(I$(Y),X+1,1))
1025 LINE (X*22+66,Y*22+34)-(X*22+84,Y*22+52),F,BF:RETURN
1030 '
1035 ' NEXT ICON
1040 '
1045 FOR B=0 TO 15:P=0:M=0
1050 FOR D=16 TO 1 STEP -1:C=VAL(MID$(I$(B),D,1)):M=M+C*2^P:P=P+1
1055 NEXT D:I(A,B)=M:NEXT B:RETURN
1060 '
1065 ' EXIT ICON EDITOR
1070 '
1075 LINE (125,150)-(355,220),0,BF:LINE (125,150)-(355,220),1,B
1080 LOCATE 155,180:PRINT"SAVE ALL CHANGES TO SD CARD?"
1085 K$=UCASE$(INKEY$):IF K$="N" THEN 1100 ELSE IF K$<>"Y" THEN 1085
1090 OPEN "ICONS.DAT" FOR OUTPUT AS #1
1095 FOR X=0 TO 31:FOR Y=0 TO 15:PRINT #1,I(X,Y):NEXT Y:NEXT X:CLOSE #1
1100 GOSUB 1145:GOTO 350
1105 '
1110 ' SUBROUTINE: DISPLAY ICON (V,W = ICON CO-ORDINATES)
1115 '
1120 LINE (V-2,W-4)-(V+37,W+35),1,B:LINE (V,W)-(V+35,W+33),0,BF
1125 FOR R=0 TO 15:FOR S=1 TO 16
1130 IF MID$(I$(R),S,1)="1" THEN LINE (S*2+V,R*2+W)-(S*2+V+1,R*2+W+1),1,BF
1135 NEXT S:NEXT R:RETURN
1140 '
1145 ' SUBROUTINE: SCREEN TRANSITION
1150 '
1155 FOR X=5 TO 240 STEP 5
1160 LINE (240-X,0)-(245-X,432),1,BF:LINE (240+X,0)-(235+X,432),1,BF
1165 LINE (245-X,0)-(250-X,432),0,BF:LINE (230+X,0)-(235+X,432),0,BF
1170 PAUSE 5:NEXT X:CLS:RETURN
1175 IF K$="8" AND Y>52 THEN Y=Y-90:A=A-5
1180 '
1185 ' SUBROUTINE: PLOT ICON
1190 '
1195 FOR B=0 TO 15:Z=X:C=I(ICON,B)
1200 D=C*.5
1205 IF D=0 THEN 1220
1210 IF D>INT(D) THEN LINE (Z,Y)-(Z+1,Y+1),1,B:C=(C-1)/2:Z=Z-2:GOTO 1200
1215 Z=Z-2:C=D:GOTO 1200
1220 Y=Y+2:NEXT B:RETURN
